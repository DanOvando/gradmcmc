model {

    ## priors
    for(i in 1:4) {
        b0[i]~dnorm(0,.1);
        b1[i]~dnorm(0,.1);
    }
    for(v in 1:(K-1)) {a[v]~dnorm(0, .1);}
    a1~dnorm(0,.1);
    sigmaphi~dt(0,1,2)I(0,5);
    sigmayearphi~dt(0,1,2)I(0,3);
    sigmap~dt(0,1,2)I(0,5);

    ## random effects; non-centered
    for(g in 1:nfam){
        fameffphi[g]~dnorm(0,1);
        fameffp[g]~dnorm(0,1);
    }
    for(ye in 1:4){
        yeareffphi[ye]~dnorm(0,1);
    }

    ## derived parameters
    for(i in 1:I){ ## loop over each individual
        ## calculate phi as a function of fixed and random effects
        for(t in 1:(K-1)) {
            logit(phi[i,t]) <- a[t]+ a1*carez[i]+ sigmayearphi*yeareffphi[year[i]]+
                                      sigmaphi*fameffphi[family[i]];
        }
        ## calculate p as a function of fixed and random effectsa
        p[i,1] <- 1;  ## first occasion is marking occasion
        for(t in 2:K){
            logit(p[i,t]) <- b0[year[i]] + b1[year[i]]*agec[t]+
                                    sigmap*fameffp[family[i]];
        }
    }
    ## probabilitiy of never being seen after last observation. ind here is
    ## a reverse index so this loop goes from K:2, recursively calculating
    ## backward.
    for(i in 1:I){
        chi[i,K+1] <- 1.0;
        for(j in ind){
            chi[i,j] <- (1-phi[i,j-1]) + phi[i,j-1]*(1-p[i,j])*chi[i,j+1];
        }
        chi[i,1] <- (1 - p[i,1]) * chi[i,2];
   }

    ## calculate likelihood of data
    for(i in 1:I){
        ## probability of survival, known alive since k<last
	for(t in 2:last[i]){
            ones[i,t]~dbern(phi[i,t-1])
	}
	## probability of observation given known alive
	for(t in 1:last[i]){
            CH[i,t]~dbern(p[i,t])
	}
	## probability of no observations after time period last
	ones2[i]~dbern(chi[i,last[i]+1])
    }
}

